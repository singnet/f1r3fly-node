new
  rl(`rho:registry:lookup`), ASIVaultCh,
  stdout(`rho:io:stdout`), log
in {

  rl!(`rho:rchain:asiVault`, *ASIVaultCh) |
  for (@(_, ASIVault) <- ASIVaultCh) {

    log!(("3.transfer_funds.rho")) |

    // REPLACE THE ASI ADDRESSES HERE vvv
    match (
      "%FROM",
      "%TO",
      %AMOUNT
    ) {
      (from, to, amount) => {

        new vaultCh, asiVaultkeyCh, deployerId(`rho:rchain:deployerId`) in {
          @ASIVault!("findOrCreate", from, *vaultCh) |
          @ASIVault!("deployerAuthKey", *deployerId, *asiVaultkeyCh) |
          for (@(true, vault) <- vaultCh; key <- asiVaultkeyCh) {

            log!(("Beginning transfer of ", amount, "ASI from", from, "to", to)) |

            new resultCh in {
              @vault!("transfer", to, amount, *key, *resultCh) |
              for (@result <- resultCh) {
                match result{
                  (true, _) =>{log!("%LOG_MARKER Successfully reason: ${reason}" %%{"reason": "Nil"})}
                  (false, reason) => {log!("%LOG_MARKER Failing reason: ${reason}" %%{"reason": reason})}
                }
              }
            }
          }
        }
      }
    }
  } |

  contract log(@data) = {
    @"DEMO"!(data) | stdout!(data)
  }
}
