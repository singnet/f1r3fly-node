new
  rl(`rho:registry:lookup`), ASIVaultCh,
  vaultCh, balanceCh,
  stdout(`rho:io:stdout`)
in {

  rl!(`rho:rchain:asiVault`, *ASIVaultCh) |
  for (@(_, ASIVault) <- ASIVaultCh) {

    stdout!(("2.check_balance.rho")) |

    // REPLACE THE ASI ADDRESS HERE vvv
    match "%ASI_ADDR" {
      asiAddress => {

        stdout!(("Accessing vault at ASIAddress", asiAddress)) |

        // most ASIVault methods return an `Either[String, A] = (false, String) \/ (true, A)`
        @ASIVault!("findOrCreate", asiAddress, *vaultCh) |
        for (@(true, vault) <- vaultCh) {

          stdout!("Obtained vault, checking balance") |

          @vault!("balance", *balanceCh) |
          for (@balance <- balanceCh) {

            stdout!(("Balance is", balance))
          }
        }
      }
    }

  }

}
