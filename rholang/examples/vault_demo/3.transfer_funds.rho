new
  rl(`rho:registry:lookup`), ASIVaultCh,
  stdout(`rho:io:stdout`)
in {

  rl!(`rho:rchain:asiVault`, *ASIVaultCh) |
  for (@(_, ASIVault) <- ASIVaultCh) {

    stdout!(("3.transfer_funds.rho")) |

    // REPLACE THE ASI ADDRESSES HERE vvv
    match (
      "%FROM",
      "%TO",
      100
    ) {
      (from, to, amount) => {

        new vaultCh, targetVaultCh, asiVaultkeyCh, deployerId(`rho:rchain:deployerId`) in {
          @ASIVault!("findOrCreate", from, *vaultCh) |
          // make sure the target vault it created and the transfer would be done
          @ASIVault!("findOrCreate", to, *targetVaultCh) |
          @ASIVault!("deployerAuthKey", *deployerId, *asiVaultkeyCh) |
          for (@(true, vault) <- vaultCh & key <- asiVaultkeyCh & @(true, _) <- targetVaultCh) {

            stdout!(("Beginning transfer of ", amount, "ASI from", from, "to", to)) |

            new resultCh in {
              @vault!("transfer", to, amount, *key, *resultCh) |
              for (@result <- resultCh) {

                stdout!(("Finished transfer of ", amount, "ASI to", to, "result was:", result))
              }
            }
          }
        }
      }
    }
  }

}
